
// PDDL DOMAIN
domain:        LPAR DEFINE domain_def [requirements] [types] [constants] [predicates] [functions] structure_def* RPAR
domain_def:    LPAR DOMAIN NAME RPAR

requirements:              LPAR REQUIREMENTS require_key+ RPAR

types:                 LPAR TYPES typed_list_name RPAR
constants:             LPAR CONSTANTS typed_list_name RPAR
predicates:            LPAR PREDICATES atomic_formula_skeleton+ RPAR
functions:             LPAR FUNCTIONS f_typed_list_atomic_function_skeleton RPAR

atomic_formula_skeleton:   LPAR NAME typed_list_variable RPAR

f_typed_list_atomic_function_skeleton: atomic_function_skeleton+
                                     | (atomic_function_skeleton+ TYPE_SEP f_type_def)+ atomic_function_skeleton*

?f_type_def: TYPE_NUMBER

atomic_function_skeleton:  LPAR NAME typed_list_variable RPAR

?structure_def:    action_def | derived_predicates
action_def:        LPAR ACTION NAME PARAMETERS action_parameters action_body_def RPAR
action_parameters: LPAR typed_list_variable RPAR
?action_body_def:  [PRECONDITION emptyor_pregd] [EFFECT emptyor_effect]
derived_predicates: LPAR DERIVED atomic_formula_skeleton gd RPAR

// preconditions
emptyor_pregd:     LPAR RPAR
             |     gd
gd:                atomic_formula_term
  |                LPAR OR gd* RPAR
  |                LPAR NOT gd RPAR
  |                LPAR AND gd* RPAR
  |                LPAR IMPLY gd gd RPAR
  |                LPAR EXISTS LPAR typed_list_variable RPAR gd RPAR
  |                LPAR FORALL LPAR typed_list_variable RPAR gd RPAR
  |                LPAR binary_comp f_exp f_exp RPAR

// effects
emptyor_effect:    LPAR RPAR
              |    effect
effect:            LPAR AND c_effect* RPAR
      |            c_effect

c_effect:          LPAR FORALL LPAR typed_list_variable RPAR effect RPAR
        |          LPAR WHEN gd cond_effect RPAR
        |          LPAR ONEOF effect+ RPAR
        |          p_effect
p_effect:          LPAR NOT atomic_formula_term RPAR
        |          atomic_formula_term
        |          num_effect
cond_effect:       LPAR AND p_effect* RPAR
           |       p_effect
num_effect:        LPAR assign_op f_head f_exp RPAR

atomic_formula_term:   LPAR predicate term* RPAR
                   |   LPAR EQUAL_OP term term RPAR
?term:  constant
     |  variable
?predicate: NAME
constant: NAME

?binary_comp: GREATER_OP
            | LESSER_OP
            | EQUAL_OP
            | GREATER_EQUAL_OP
            | LESSER_EQUAL_OP

?binary_op: multi_op
          | MINUS
          | DIVIDE

?multi_op: TIMES
         | PLUS

num_literal: NUMBER

f_exp:      num_literal
      |     LPAR binary_op f_exp f_exp RPAR
      |     LPAR multi_op f_exp f_exp+ RPAR
      |     LPAR MINUS f_exp RPAR
      |     f_head

f_head:     NAME
      |     LPAR NAME term* RPAR

?assign_op: ASSIGN
          | SCALE_UP
          | SCALE_DOWN
          | INCREASE
          | DECREASE

typed_list_variable:   variable*
                   |   (variable+ TYPE_SEP type_def)+ variable*
?variable: "?" NAME

typed_list_name:   NAME*
               |   (NAME+ TYPE_SEP primitive_type)+ NAME*
type_def:          LPAR EITHER primitive_type+ RPAR
        |          primitive_type
?primitive_type:   NAME
               |   OBJECT


// PDDL PROBLEM
problem:           LPAR DEFINE problem_def problem_domain [requirements] [objects] init goal [metric_spec] RPAR
problem_def:       LPAR PROBLEM NAME RPAR
problem_domain:    LPAR DOMAIN_P NAME RPAR


objects:   LPAR OBJECTS typed_list_name RPAR

init:                  LPAR INIT init_el* RPAR
init_el:               literal_name
       |               LPAR EQUAL_OP basic_function_term num_literal RPAR

literal_name:          atomic_formula_name
            |          LPAR NOT atomic_formula_name RPAR

basic_function_term:   NAME
                   |   LPAR NAME NAME* RPAR

atomic_formula_name:   LPAR predicate NAME* RPAR

goal:  LPAR GOAL gd RPAR

metric_spec: LPAR METRIC optimization metric_f_exp RPAR

?optimization: MAXIMIZE
             | MINIMIZE

metric_f_exp: LPAR binary_op metric_f_exp metric_f_exp RPAR
            | LPAR multi_op metric_f_exp metric_f_exp+ RPAR
            | LPAR MINUS metric_f_exp RPAR
            | basic_function_term
            | NUMBER

DOMAIN_P: ":domain"
PROBLEM: "problem"
OBJECTS: ":objects"
INIT: ":init"
GOAL: ":goal"


// PDDL PLAN
plan:  ground_action*
ground_action: LPAR predicate NAME* RPAR



%ignore /\s+/
%ignore COMMENT


NAME: /[a-zA-Z][a-zA-Z0-9-_]*/

?require_key: TYPING
            | STRIPS
            | EQUALITY
            | NON_DETERMINISTIC
            | NEG_PRECONDITION
            | DIS_PRECONDITION
            | EXISTENTIAL_PRECONDITIONS
            | UNIVERSAL_PRECONDITIONS
            | QUANTIFIED_PRECONDITIONS
            | ADL
            | DERIVED_PREDICATES
            | CONDITIONAL_EFFECTS
            | NUMERIC_FLUENTS
            | FLUENTS
            | ACTION_COSTS

COMMENT: /;[^\n]*/

DEFINE: "define"
DOMAIN: "domain"
REQUIREMENTS: ":requirements"
TYPES: ":types"
CONSTANTS: ":constants"
PREDICATES: ":predicates"
FUNCTIONS: ":functions"
ACTION: ":action"
PARAMETERS: ":parameters"
PRECONDITION: ":precondition"
EFFECT: ":effect"
DERIVED: ":derived"
FORALL: "forall"
EXISTS: "exists"
WHEN: "when"
OBJECT: "object"
AND: "and"
OR: "or"
NOT: "not"
IMPLY: "imply"
EITHER: "either"
ONEOF: "oneof"
EQUAL_OP: "="
GREATER_EQUAL_OP: ">="
GREATER_OP: ">"
LESSER_EQUAL_OP: "<="
LESSER_OP: "<"
MINUS: "-"
PLUS: "+"
TIMES: "*"
DIVIDE: "/"
METRIC: ":metric"
ASSIGN: "assign"
SCALE_UP: "scale-up"
SCALE_DOWN: "scale-down"
INCREASE: "increase"
DECREASE: "decrease"
MAXIMIZE: "maximize"
MINIMIZE: "minimize"
TOTAL_COST: "total-cost"
NUMBER: /[0-9]+(\.[0-9]+)*/

// available requirements
STRIPS: ":strips"
TYPING: ":typing"
EQUALITY: ":equality"
ADL: ":adl"
NON_DETERMINISTIC: ":non-deterministic"
NEG_PRECONDITION: ":negative-preconditions"
DIS_PRECONDITION: ":disjunctive-preconditions"
DERIVED_PREDICATES: ":derived-predicates"
CONDITIONAL_EFFECTS: ":conditional-effects"
EXISTENTIAL_PRECONDITIONS: ":existential-preconditions"
UNIVERSAL_PRECONDITIONS: ":universal-preconditions"
QUANTIFIED_PRECONDITIONS: ":quantified-preconditions"
NUMERIC_FLUENTS: ":numeric-fluents"
FLUENTS: ":fluents"
ACTION_COSTS: ":action-costs"

// others
LPAR : "("
RPAR : ")"
TYPE_SEP: "-"
TYPE_NUMBER: "number"
